plugins {
    id 'com.android.application'
}

android {
    namespace = "com.sdl.gpu.xr.examples"
    compileSdkVersion 35
    ndkVersion = "27.0.12077973"
    
    buildFeatures {
        prefab true  // Enable Prefab for Khronos OpenXR loader AAR
    }
    
    defaultConfig {
        applicationId "com.sdl.gpu.xr.examples"
        minSdkVersion 29  // Quest 2 minimum
        targetSdkVersion 32  // Meta recommends 32 for Quest
        versionCode 1
        versionName "1.0"
        
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_PLATFORM=android-29", "-DANDROID_STL=c++_shared", "-DSDL_GPU_OPENXR=ON"
                abiFilters 'arm64-v8a'  // Quest is arm64 only
            }
        }
        
        ndk {
            abiFilters 'arm64-v8a'
        }
    }
    
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            jniDebuggable true
        }
    }
    
    sourceSets.main {
        // Shader assets are in our own Content folder
        assets.srcDirs = ['../../Content']
        jniLibs.srcDir 'libs'
    }
    
    externalNativeBuild {
        cmake {
            path 'jni/CMakeLists.txt'
        }
    }
    
    lint {
        abortOnError = false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    // Khronos OpenXR loader - properly exports xrGetInstanceProcAddr unlike Meta's forwardloader
    implementation 'org.khronos.openxr:openxr_loader_for_android:1.1.54'
}
